plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'idea'
}

apply plugin: 'com.github.johnrengelman.shadow'

group 'org.rundeck.cli.ext'

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

version=scmVersion.version
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        url "https://rundeck.bintray.com/maven"
    }
}

ext.toolbeltVersion = '0.2.12'
ext.rdVersion='1.2.3'
ext.rundeckVersion='3.2.7-20200515'
shadowJar{

}

dependencies {

    compileOnly "org.projectlombok:lombok:1.18.24"
    annotationProcessor "org.projectlombok:lombok:1.18.24"

    shadow "org.rundeck.cli:rd-cli-lib:${rdVersion}"
    shadow "org.rundeck.cli-toolbelt:toolbelt-jewelcli:$toolbeltVersion"
    shadow "org.yaml:snakeyaml:1.26"

    implementation ("org.rundeck:rundeck-authz-yaml:$rundeckVersion"){
        exclude(group:'org.yaml', module:'snakeyaml')
    }
    implementation "org.rundeck:rundeck-authz-api:$rundeckVersion"
    implementation "org.rundeck:rundeck-authz-core:$rundeckVersion"

    testImplementation "org.rundeck.cli:rd-cli-lib:${rdVersion}"
    testImplementation "org.rundeck.api:rd-api-client:${rdVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "org.codehaus.groovy:groovy-all:2.5.7"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

publishing{
    publications {
        rdExtLib(MavenPublication) { publication ->
            project.shadow.component(publication)
            version = project.version
            pom {
                name = "RD Extension: ${project.name}".toString()
                description = "RD Extension - ${project.name}".toString()
                url = 'https://github.com/rundeck/rd-ext-acl'

                developers {
                    developer {
                        id = 'gschueler'
                        name = 'Greg Schueler'
                        email = 'greg@rundeck.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rundeck/rd-ext-acl.git'
                    developerConnection = 'scm:git:ssh://github.com/rundeck/rd-ext-acl.git'
                    url = 'https://github.com/rundeck/rd-ext-acl'
                }
            }
        }
    }
}
assemble.dependsOn shadowJar
